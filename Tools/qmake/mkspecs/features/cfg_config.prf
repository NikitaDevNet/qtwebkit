# -------------------------------------------------------------------
# Global 'configure' file for WebKit.
# For options see cfg_config.pri
# Defines of conditions 'cfg_*'.
#
# See 'Tools/qmake/README' for an overview of the build system
# -------------------------------------------------------------------

include(cfg_config.pri)

defineTest(dwarning) {
# Comment the lines for disable debug warnings.
#    warning($${EOL})
#    warning(=========== DEBUG WARNING ==========$${EOL})
#    warning($${EOL})

#    warning("  $$1")

#    for(item, 2) {
#        warning("      $$item")
#    }

#    warning($${EOL})
#    warning(------- END OF DEBUG WARNING -------$${EOL})
#    warning($${EOL})
#    warning($${EOL})
}

defineTest(cfg_build?) {
    contains(CFG_BUILD_FEATURES, CFG_BUILD_$$upper($$1)=1): return(true)
    return(false)
}

defineTest(cfg_have?) {
    contains(CFG_FEATURE_DEFAULTS, CFG_HAVE_$$upper($$1)=1): return(true)
    return(false)
}

defineTest(cfg_use?) {
    contains(CFG_FEATURE_DEFAULTS, CFG_USE_$$upper($$1)=1): return(true)
    return(false)
}

defineTest(cfg_enable?) {
    contains(CFG_FEATURE_DEFAULTS, CFG_ENABLE_$$upper($$1)=1): return(true)
    return(false)
}

defineReplace(cfgMinimalQtDefines) {
    unset(cfg_defines)

    for(cfg_define, CFG_MINIMAL_QT_DEFINES) {
        enabledFeature = $$find(cfg_define, =1$)
        isEmpty(enabledFeature): next()

        enabledFeature ~= s/=1$//
        cfg_defines += $$enabledFeature
    }

    return($$cfg_defines)
}
