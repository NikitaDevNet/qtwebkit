# -------------------------------------------------------------------
# Global 'configure' file for WebKit.
# For options see cfg_config.pri
# Defines of conditions 'cfg_*'.
#
# See 'Tools/qmake/README' for an overview of the build system
# -------------------------------------------------------------------

include(cfg_config.pri)

defineTest(cfg_dwarning) {

    contains(CFG_DEBUG_WARNING, ON) {
#        warning($${EOL})
#        warning(=========== DEBUG WARNING ==========$${EOL})
#        warning($${EOL})

        warning("  $$1")

        for(item, 2) {
            warning("      $$item")
        }

#        warning($${EOL})
#        warning(------- END OF DEBUG WARNING -------$${EOL})
#        warning($${EOL})
#        warning($${EOL})
    }
}

defineTest(cfg_dwarning_file_in) {
    cfg_dwarning(=========== ENTERING $$1 ==========$${EOL})
}

defineTest(cfg_dwarning_file_out) {
    cfg_dwarning(=========== LAEVING $$1 ==========$${EOL})
}


defineTest(cfg_build?) {
    cfg_dwarning(=== cfg_build?($$1) ===$${EOL})
    contains(CFG_BUILD_FEATURES, CFG_BUILD_$$upper($$1)=1): return(true)
    return(false)
}

defineTest(cfg_have?) {
    cfg_dwarning(=== cfg_have?($$1) ===$${EOL})
    contains(CFG_FEATURE_DEFAULTS, HAVE_$$upper($$1)=1): return(true)
    return(false)
}

defineTest(cfg_use?) {
    cfg_dwarning(=== cfg_use?($$1) ===$${EOL})
    contains(CFG_FEATURE_DEFAULTS, USE_$$upper($$1)=1): return(true)
    return(false)
}

defineTest(cfg_enable?) {
    cfg_dwarning(=== cfg_enable?($$1) ===$${EOL})
    contains(CFG_FEATURE_DEFAULTS, ENABLE_$$upper($$1)=1): return(true)
    return(false)
}

defineReplace(cfg_MinimalQtDefines) {
    unset(cfg_defines)

    for(cfg_define, CFG_MINIMAL_QT_DEFINES) {
        enabledFeature = $$find(cfg_define, =1$)
        isEmpty(enabledFeature): next()

        enabledFeature ~= s/=1$//
        cfg_defines += $$enabledFeature
    }

    return($$cfg_defines)
}


defineTest(cfg_AllInOneSourcesOff) {
    collection = $$CFG_ALL_IN_ONE_SOURCES_OFF

    for(collectionFile, collection) {
        lines = $$cat($$_PRO_FILE_PWD_/$$collectionFile, lines)
        for(line, lines) {
            line = $$find(line, "^.+include \"(.*)\"")
            isEmpty(line): next()
            line ~= s/^.+include \"(.*)\"/\\1/
            source = $$dirname(collectionFile)/$$line
            contains(SOURCES, $$source) {
                SOURCES -= $$source
                tookFromSources = true
            }
        }
    }
}
